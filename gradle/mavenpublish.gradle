apply plugin: 'signing'

afterEvaluate {
    signing {
        required { !project.VERSION_NAME.endsWith('SNAPSHOT') }

        def signingKey = project.ORG_GRADLE_PROJECT_signingKey
        def signingPassword = project.ORG_GRADLE_PROJECT_signingPassword
        useInMemoryPgpKeys(signingKey, signingPassword)

        sign publishing.publications.release
        sign configurations.archives
    }

    publishing {
        publications {
            release(MavenPublication) {
                groupId = project.GROUP
                artifactId = project.POM_ARTIFACT_ID
                version = project.VERSION_NAME

                from(components['release'])

                pom {
                    name.set('VK SDK Android')
                    description.set(project.POM_DESCRIPTION)
                    url.set('https://github.com/vksdk/vk-sdk-android')

                    licenses {
                        license {
                            name.set('MIT License')
                            url.set('https://raw.githubusercontent.com/vksdk/vk-sdk-android/master/LICENSE')
                            distribution.set('repo')
                        }
                    }
                    developers {
                        developer {
                            id.set('PeterSamokhin')
                            name.set('Peter Samokhin')
                            url.set('https://github.com/petersamokhin')
                            email.set('contact+vksdk@petersamokhin.com')
                        }
                    }

                    scm {
                        url.set('https://github.com/vksdk/vk-sdk-android')
                        connection.set('scm:git:git://github.com/vksdk/vk-sdk-android.git')
                        developerConnection.set('scm:git:ssh://git@github.com/vksdk/vk-sdk-android.git')
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = project.REPOSITORY_URL_MAVEN_STAGING_DEFAULT
                def snapshotsRepoUrl = project.REPOSITORY_URL_MAVEN_SNAPSHOT_DEFAULT

                url = project.VERSION_NAME.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                if (!project.VERSION_NAME.endsWith('SNAPSHOT')) {
                    credentials {
                        username = project.ORG_GRADLE_PROJECT_SONATYPE_NEXUS_USERNAME
                        password = project.ORG_GRADLE_PROJECT_SONATYPE_NEXUS_PASSWORD
                    }
                }
            }
        }
    }
}
